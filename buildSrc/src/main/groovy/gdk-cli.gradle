/*
 * Copyright 2025 Oracle and/or its affiliates
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'gdk-module'
    id 'io.micronaut.application'
    id 'com.gradleup.shadow'
}

dependencies {
    annotationProcessor mnLibs.picocli.codegen

    compileOnly mnLibs.graal

    implementation projects.gdkCliCore

    runtimeOnly libs.bouncycastle.bcpkix
    runtimeOnly libs.bouncycastle.bcprov
    runtimeOnly libs.jansi
    runtimeOnly libs.jline
    runtimeOnly mnLibs.slf4j.nop
}

configurations.configureEach {
    exclude module: 'logback-classic'
}

micronaut {
    version libs.versions.micronaut.starter.get()
    processing {
        incremental true
        annotations 'cloud.graal.gdk.*'
    }
}

tasks.named('shadowJar') {
    mergeServiceFiles()
}

tasks.named('shadowDistZip') {
    enabled = false
}
tasks.named('shadowDistTar') {
    enabled = false
}

tasks.register('copyShadowJar', Sync) {
    from shadowJar.outputs
    into "${project.rootProject.buildDir}/libs"
    rename { String fileName -> 'cli.jar' }
}
