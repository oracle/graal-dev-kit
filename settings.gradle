/*
 * Copyright 2023 Oracle and/or its affiliates
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rootProject.name = 'gdk'

file('.').eachDir {
    if (it.name.startsWith('gdk-')) {
        include it.name
    }
}

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

String[] fileLines = new File(rootProject.projectDir.absoluteFile, 'gradle/templates.versions.toml').text.split('\n')
String micronautPlatformVersion = fileLines.find { it.contains('micronaut-platform') }.split('=')[1].replace('"', '').strip()

dependencyResolutionManagement {
    repositories {
        maven { url 'https://maven.oracle.com/public' }
        mavenCentral()
        gradlePluginPortal()
    }
    versionCatalogs {
        mnLibs {
            from "io.micronaut.platform:micronaut-platform:$micronautPlatformVersion"
        }
        templateLibs {
            from(files('gradle/templates.versions.toml'))
        }
    }
}
